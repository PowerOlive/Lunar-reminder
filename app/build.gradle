apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

def version = "1.0.0"
def gitCommits = Integer.parseInt('git rev-list --count HEAD'.execute([], project.rootDir).text.trim())

def keystorePSW = ''
def keystoreAlias = ''
def keystoreAliasPSW = ''
// default keystore file, PLZ config file path in local.properties
def keyFile = file('s.keystore.temp')

Properties properties = new Properties()
// local.properties file in the root director
properties.load(project.rootProject.file('local.properties').newDataInputStream())
def keystoreFilepath = properties.getProperty("keystore.path")

if (keystoreFilepath) {
    keystorePSW = properties.getProperty("keystore.password")
    keystoreAlias = properties.getProperty("keystore.alias")
    keystoreAliasPSW = properties.getProperty("keystore.alias_password")
    keyFile = file(keystoreFilepath)
}

android {
    compileSdkVersion 28
    buildToolsVersion '28.0.3'
    signingConfigs {
        myConfig {
            storeFile keyFile
            storePassword keystorePSW
            keyAlias keystoreAlias
            keyPassword keystoreAliasPSW
        }
    }
    defaultConfig {
        applicationId "gedoor.kunfei.lunarreminder"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 10000 + gitCommits
        versionName version
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        project.ext.set("archivesBaseName", "LunarReminder-" + version)
    }

    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.myConfig
        }
        release {
            signingConfig signingConfigs.myConfig
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    configurations.all {
        resolutionStrategy.force 'com.google.code.findbugs:jsr305:1.3.9'
        exclude group: "org.apache.httpcomponents", module: "httpclient"
    }

}

dependencies {
    //fireBase
    implementation 'com.google.firebase:firebase-core:16.0.8'
    implementation 'com.crashlytics.sdk.android:crashlytics:2.9.9'

    //androidx
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.appcompat:appcompat:1.1.0-alpha04'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'

    //material
    implementation 'com.google.android.material:material:1.1.0-alpha05'

    //bind view
    implementation 'com.jakewharton:butterknife:10.0.0'
    annotationProcessor 'com.jakewharton:butterknife-compiler:10.0.0'

    //google
    implementation 'com.google.apis:google-api-services-calendar:v3-rev239-1.22.0'
    implementation 'com.google.android.gms:play-services-auth:16.0.1'
    implementation 'com.google.api-client:google-api-client-android:1.22.0'
    implementation 'com.google.http-client:google-http-client-gson:1.22.0'

    //other
//    implementation 'pub.devrel:easypermissions:1.1.1'
//    implementation 'cn.carbs.android:IndicatorView:1.0.2'
//    implementation 'cn.carbswang.android:NumberPickerView:1.1.0'
}
apply plugin: 'com.google.gms.google-services'
